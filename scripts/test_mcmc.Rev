########################################################
#
# RevBayes Example: Non-reversible to reversible substitution model on strict clock tree
#
#
# authors: Sebastian Hoehna & John Huelsenbeck
#
########################################################

ANALYSIS_NAME = args[1]
MODEL_NAME    = args[2]


#######################
# Reading in the Data #
#######################

# Import the molecular sequences #
# this file contains only the taxa for which sequence data are available #
aln <- readDiscreteCharacterData("data/"+ANALYSIS_NAME+".nex")

## helpers
n_taxa <- aln.size()
taxa <- aln.taxa()


# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()

##################################################
# Fixed topology with exponential branch lengths #
##################################################

rooted_tree <- readTrees("data/"+ANALYSIS_NAME+"_rooted.tre", treetype="clock")[1]

outgroup <- clade("Diplolaemus_darwinii")

tree ~ dnUniformTopologyBranchLength( branchLengthDistribution=dnExp(10.0), taxa,
                                      rooted=TRUE,
                                      outgroup )
tree.setValue( rooted_tree )

TL := tree.treeLength()

moves.append( mvBranchLengthScale( tree, weight=n_taxa*2-1 ) )




#################################
# nucleotide substitution model #
#################################

source("scripts/mod_"+MODEL_NAME+".Rev")


# Create the substitution model and clamp with our observed DNA data #
phySeq ~ dnPhyloCTMC(tree=tree, Q=Q_gen, siteRates=rates, type="DNA")
phySeq.clamp(aln)



########
# MCMC #
########

# initialize the model object #
mymodel = model(Q_gen)


# Create a vector of monitors #
# 1. for the full model #
monitors.append( mnModel(filename="output/"+ANALYSIS_NAME+"_"+MODEL_NAME+".log", printgen=10) )

# 2. the tree #
monitors.append( mnFile(filename="output/"+ANALYSIS_NAME+"_"+MODEL_NAME+".trees", printgen=10, tree) )

# 3. and a few select parameters to be printed to the screen #
monitors.append( mnScreen(printgen=10) )

# Initialize the MCMC object #
mymcmc = mcmc(mymodel, monitors, moves, nruns=4, combine="mixed")

# Run the MCMC #
mymcmc.run(generations=10000, tuningInterval=200, underPrior=!TRUE)

mymcmc.operatorSummary()

# Quit RevBayes #
q()
